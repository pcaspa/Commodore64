1 cut and paste in CMB PRG Studio
2 rem {pound}real xl,xu,yl,yu,xinc,yinc,nreal,nimg,rz,iz,r2z,i2z,
3 rem {pound}byte reps,width,weight,k,c,lb,hb,a
4 rem {pound}word x
10 print"{clear}{space*9}mandelbrot explorer{down*2}"
11 print"1. standard"
12 print"2. sec.bulb x:-0.35,0.1 y:-1.1,1.1
13 PRINT"3. neg axis x:-1.55,-1.25 y:0.15,-0.15"
14 PRINT"4. cool arm x:-1.3,-1.1 y:0.45,0.25"
15 PRINT"5. cool arm x:0.25,0.45 y:0.7,0.5"
16 PRINT"6. cool arm x:-0.65,-0.45 y:0.72,0.52"
17 PRINT"7. custom{space*3}set your own coordinates"
50 INPUT "{down}select set";I
60 ON I GOSUB 70,75,80,85,90,95,110
65 GOTO 120
70 XL =-2.000:XU =0.500
71 YL =-1.100:YU=1.100:RETURN
75 XL =-0.35:XU =0.1
76 YL =1.1:YU=0.65:RETURN
80 XL =-1.55:XU =-1.25
81 YL =0.15:YU=-0.15:RETURN
85 XL =-1.3:XU =-1.1
86 YL =0.45:YU=0.25:RETURN
90 XL =0.25:XU =0.45
91 YL =0.7:YU=0.5:RETURN
95 XL =-0.65:XU =-0.45
96 YL =0.72:YU=0.52:RETURN
110 INPUT"-x (default -2.0)";XL
112 INPUT"+x (default +0.5)";XU
114 INPUT"-y (default -1.1)";YL
116 INPUT"+y (default +1.1)";YU
118 RETURN
120 RE =20
125 WI=160:HE=200
130 XI =(XU-XL)/WI
140 YI =(YU-YL)/HE
200 REM MAIN ROUTINE
205 PRINT "{clear}"
207 GOSUB 500
210 FOR J = 0 TO HE - 1
220 FOR I = 0 TO WI -1
230 GOSUB 300
240 GOSUB 600
260 NEXT
270 NEXT
290 GET A$: IF A$ = ""THEN 290
299 GOSUB700:GOTO10
300 REM CALCULATE MANDELBROT
312 NR=XL+(I*XI)
313 NI=YL+(J*YI)
315 RZ=0:IZ=0
316 R2=0:I2=0:K=0
320 K=K+1
330 R2=(RZ*RZ)-(IZ*IZ)
340 I2=2*RZ*IZ
350 RZ=R2+NR
360 IZ=I2+NI
370 IF(RZ*RZ+IZ*IZ)>4 THEN395
390 IFK<RETHEN320
395 IF K<7 THEN C=3:RETURN
396 IF K<9 THEN C=2:RETURN
397 IF K<14THEN C=1:RETURN
398 C=0
400 RETURN
500 REM CLEAR SCREEN
530 POKE 53272,29:POKE 53265,59
531 POKE53270,PEEK(53270)OR16
532 POKE 53281, 0
541 GOSUB 800
549 REM SET SCREEN COLORS TO GRAYS
550 FOR R=1024 TO 2023:POKE R,125:NEXT:REM 207
560 FOR R=55296TO56295:POKE R,14:NEXT
598 RETURN
599 REM DRAW HI-RES
600 CH=INT(I/4)*8
605 REM K=HEIGHT-1-J : REM HERE WE DO THE MIRRORING
610 P =8192 + INT(J/8)*320 + CH+(JAND7)
615 REM P2=8192 + INT(K/8)*320 + CH+(KAND7)
625 LB=2^(7-(1+2*(IAND3))):REM MULTICOLOR LOW BIT
626 HB=2^(7-(2*(IAND3))): REM HIGH BIT
630 IF C=0 THEN RETURN
635 A=PEEK(P)
640 IF C=1 THEN POKEP,A OR LB:RETURN
641 REM IF C=1 THEN POKEP2,A OR LB:RETURN
650 IF C=2 THEN POKEP,A OR HB:RETURN
651 REM IF C=2 THEN POKEP2,A OR HB:RETURN
655 A=A OR LB
660 POKEP,A
670 POKEP, A OR HB
680 RETURN
700 REM RETURN TO NORMAL
705 POKE 53280, 14:POKE 53281,6
740 POKE 53272,21:POKE 53265,27
750 POKE 53270,PEEK(53270)AND239
760 RETURN
799 REM ERASE THE HIRES SCREEN
800 FOR X=8192TO16192:POKEX,0:NEXT:RETURN
